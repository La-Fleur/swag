<html>
<head><title> "Super Fast Polys" by JEROEN BOUWENS</title><link rel="STYLESHEET" type="text/css" href="../swagsource.css"></head>
<body><p align="center"><b>[</b><a href="index.html">Back to GRAPHICS SWAG index</a><b>]</b>&nbsp;&nbsp;<b>[</b><a href="../index.html">Back to Main SWAG index</a><b>]</b>&nbsp;&nbsp;<b>[</b><a href="0181.PAS">Original</a><b>]</b></p><!--StartFragment--><pre><code><font color="#008000"><i>{
Ok, it's finished now, and it's pretty fast (1600-1700 poly's per second on
one of the DX2-66's in school). You can find it below. But I doubt if you'll
learn a lot from it, except asm tricks. Better is to understand the method,
and then write you own routines. But you're allowed to use it :-) (Credit
me, allright? I like that :-).

&gt; I haven't done much figure graphics(stuff like polygons, 3d,
&gt; rotating, etc...), simply because I'm still learning, and am
&gt; going slow.  But, what else can you expect, I'm only 14.  &lt;g&gt;

Well, in that case you've got enough time to learn :-). I'm 19 now, and I'm
still learning every day (In fact I've been born 10 years early, I started
programming on a ZX Spectrum (yech, 8 colors) :-). Just pay attention in
math-class. For efficient code, you need to know your math's really well!

Ok, here's the polygon-routine:
}

</i></font><font color="#FF0000"><b>Procedure </b></font>TriAngle<font color="#000080">(</font>X1<font color="#000080">,</font>Y1<font color="#000080">,</font>X2<font color="#000080">,</font>Y2<font color="#000080">,</font>X3<font color="#000080">,</font>Y3<font color="#000080">:</font>Integer<font color="#000080">; </font>Color<font color="#000080">:</font>Byte<font color="#000080">); </font><font color="#FF0000"><b>Assembler</b></font><font color="#000080">;
</font><font color="#FF0000"><b>Var </b></font>RV1<font color="#000080">,</font>RV2<font color="#000080">,</font>IF1<font color="#000080">,</font>IF2<font color="#000080">,</font>DeX1<font color="#000080">,</font>DeX2<font color="#000080">,</font>DeY1<font color="#000080">,</font>DeY2 <font color="#000080">: </font>Integer<font color="#000080">;
</font><font color="#FF0000"><b>Asm

  </b></font><font color="#FF00FF">CLI

  </font><font color="#008000"><i>{Sort by Y-value}
  </i></font><font color="#FF00FF">Mov  CX,2
@@SortLoop:
  Mov  AX,[Y2]; Cmp  AX,[Y3]; JBE  @@Skip1
  Xor  AX,[Y3]; Xor  [Y3],AX; Xor  AX,[Y3]; Mov  [Y2],AX
  Mov  AX,[X2]; Xor  AX,[X3]; Xor  [X3],AX; Xor  AX,[X3]; Mov  [X2],AX
@@Skip1:
  Mov  AX,[Y1]; Cmp  AX,[Y2]; JBE  @@Skip2
  Xor  AX,[Y2]; Xor  [Y2],AX; Xor  AX,[Y2]; Mov  [Y1],AX
  Mov  AX,[X1]; Xor  AX,[X2]; Xor  [X2],AX; Xor  AX,[X2]; Mov  [X1],AX
@@Skip2:
  Mov  AX,[Y1]; Cmp  AX,[Y3]; JBE  @@Skip3
  Xor  AX,[Y3]; Xor  [Y3],AX; Xor  AX,[Y3]; Mov  [Y1],AX
  Mov  AX,[X1]; Xor  AX,[X3]; Xor  [X3],AX; Xor  AX,[X3]; Mov  [X1],AX
@@Skip3:
  Loop @@SortLoop

  </font><font color="#008000"><i>{Calculate start-offsets}
  </i></font><font color="#FF00FF">Mov  DX,[Y1]; Shl  DX,6; Mov  BX,DX; Shl  DX,2; Add  DX,BX
  Add  DX,[X1]; Mov  SI,DX

  </font><font color="#008000"><i>{Claculate DY, and fill DeY en RefVar with it}
  {Just sorted by Y-value, so no checking for &lt;0 is needed}
  </i></font><font color="#FF00FF">Mov  AX,[Y3]; Sub  AX,[Y1]; Inc  AX; Mov  [DeY1],AX
  Mov  [RV1],AX; Mov  AX,[Y2]; Sub  AX,[Y1]; Inc  AX
  Mov  [DeY2],AX; Mov  [RV2],AX

  </font><font color="#008000"><i>{Same for DX. Possible to get a &lt;0 value, so check for that}
  </i></font><font color="#FF00FF">Mov  [IF1],1; Mov  AX,[X3]; Sub  AX,[X1]; JNC  @@SkipDXNeg1
  Neg  AX; Neg  [IF1]
@@SkipDXNeg1:
  Inc  AX; Mov  [DeX1],AX; Mov  [IF2],1; Mov  AX,[X2]
  Sub  AX,[X1]; JNC  @@SkipDXNeg2; Neg  AX; Neg  [IF2]
@@SkipDXNeg2:
  Inc  AX; Mov  [DeX2],AX

  </font><font color="#008000"><i>{Video segment in ES}
  </i></font><font color="#FF00FF">Mov  AX,$A000; Mov  ES,AX

  Mov  AL,[Color]; Mov  AH,AL; Mov  CX,[DeY2]
@@DrawLoop1:
  Push CX
  </font><font color="#008000"><i>{Draw a horizontal line}
  </i></font><font color="#FF00FF">Mov  DI,DX
  Mov  CX,SI
  Cmp  CX,DI
  JA   @@DontSwap1
  Xchg CX,DI
@@DontSwap1:
  Sub  CX,DI
  Inc  CX
  Test CX,1
  JZ   @@Even1
  StosB
@@Even1:
  Shr  CX,1
  Rep  StosW

  </font><font color="#008000"><i>{Adapt: RV1, Ofs1}
  </i></font><font color="#FF00FF">Mov  BX,[RV1]
  Sub  BX,[DeX1]
  Cmp  BX,0
  JG   @@DoNothing1
@@DoSomething1:
  Add  BX,[DeY1]
  Add  DX,[IF1]
  Cmp  BX,0
  JLE  @@DoSomething1
@@DoNothing1:
  Add  DX,320
  Mov  [RV1],BX

  </font><font color="#008000"><i>{Adapt: RV2, Ofs2}
  </i></font><font color="#FF00FF">Mov  BX,[RV2]
  Sub  BX,[DeX2]
  Cmp  BX,0
  JG   @@DoNothing2
@@DoSomething2:
  Add  BX,[DeY2]
  Add  SI,[IF2]
  Cmp  BX,0
  JLE  @@DoSomething2
@@DoNothing2:
  Add  SI,320
  Mov  [RV2],BX

  Pop  CX
  Loop @@DrawLoop1

  </font><font color="#008000"><i>{Adapt: DeY2, DeX2, RV2, IF2}
  </i></font><font color="#FF00FF">Push DX
  Mov  DX,[Y3]
  Sub  DX,[Y2]
  Inc  DX
  Mov  [DeY2],DX
  Mov  [RV2],DX
  Mov  [IF2],1
  Mov  DX,[X3]
  Sub  DX,[X2]
  JNC  @@DX2Pos
  Neg  DX
  Neg  [IF2]
@@DX2Pos:
  Inc  DX
  Mov  [DeX2],DX
  Pop  DX

  </font><font color="#008000"><i>{Draw second half of poly}
  </i></font><font color="#FF00FF">Mov  CX,[DeY2]
@@DrawLoop2:
  Push CX
  </font><font color="#008000"><i>{Draw a horizontal line}
  </i></font><font color="#FF00FF">Mov  DI,DX
  Mov  CX,SI
  Cmp  CX,DI
  JA   @@DontSwap2
  Xchg CX,DI
@@DontSwap2:
  Sub  CX,DI
  Inc  CX
  Test CX,1
  JZ   @@Even2
  StosB
@@Even2:
  Shr  CX,1
  Rep  StosW

  </font><font color="#008000"><i>{Adapt: RV1, Ofs1}
  </i></font><font color="#FF00FF">Mov  BX,[RV1]
  Sub  BX,[DeX1]
  Cmp  BX,0
  JG   @@DoNothing3
@@DoSomething3:
  Add  BX,[DeY1]
  Add  DX,[IF1]
  Cmp  BX,0
  JLE  @@DoSomething3
@@DoNothing3:
  Add  DX,320
  Mov  [RV1],BX

  </font><font color="#008000"><i>{Adapt: RV2, Ofs2}
  </i></font><font color="#FF00FF">Mov  BX,[RV2]
  Sub  BX,[DeX2]
  Cmp  BX,0
  JG   @@DoNothing4
@@DoSomething4:
  Add  BX,[DeY2]
  Add  SI,[IF2]
  Cmp  BX,0
  JLE  @@DoSomething4
@@DoNothing4:
  Add  SI,320
  Mov  [RV2],BX

  Pop  CX
  Loop @@DrawLoop2
@@Exit:
  STI
</font><font color="#FF0000"><b>End</b></font><font color="#000080">;</font><font color="#008000"><i>{NewTri3}

{
To make the source readeable again, get rid of all ; (Done to decrease size).
If you want I can explain to you how it works.
}
</i></font>
</code></pre><!--EndFragment--><p align="center"><b>[</b><a href="index.html">Back to GRAPHICS SWAG index</a><b>]</b>&nbsp;&nbsp;<b>[</b><a href="../index.html">Back to Main SWAG index</a><b>]</b>&nbsp;&nbsp;<b>[</b><a href="0181.PAS">Original</a><b>]</b></p></body>
</html>
